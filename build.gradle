plugins {
	alias(libs.plugins.spring.boot)
	id 'io.spring.dependency-management' version '1.1.7'
	id 'java-library'
	id "io.freefair.lombok" version "9.1.0"
	id 'jacoco'
	id "org.flywaydb.flyway" version "12.0.0"
//	id 'org.owasp.dependencycheck' version '9.1.0'
	id "org.sonarqube" version "7.2.2.6593"
//	id 'info.solidsoft.pitest' version '1.15.0'
}

group = 'de.thg'
version = '0.0.7-SNAPSHOT'

dependencies {

	api libs.spring.boot.starter.webmvc
	api libs.spring.boot.starter.mongodb
	implementation libs.spring.boot.starter.actuator
	implementation libs.spring.boot.starter.validation
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	implementation libs.springdoc.openapi.starter.webmvc.ui

	implementation libs.metadata.extractor
	implementation libs.commons.imaging
	implementation libs.tika.core
	api libs.dependency.check.gradle
}


java {
	toolchain.languageVersion.set(JavaLanguageVersion.of(25))
}

testing {
	suites {

		test {
			dependencies {
				implementation project()
				implementation libs.spring.boot.starter.webmvc.test
				implementation libs.spring.boot.starter.mongodb.test
				implementation libs.spring.boot.starter.actuator.test
				implementation libs.spring.boot.starter.validation.test
				implementation libs.assertj.core
				implementation libs.rest.assured
				runtimeOnly 'org.junit.platform:junit-platform-launcher'
			}

			useJUnitJupiter()
		}

		integrationTest(JvmTestSuite) {
			dependencies {
				implementation project()
				implementation libs.spring.boot.starter.webmvc.test
				implementation libs.spring.boot.testcontainers
				implementation libs.testcontainers.junit.jupiter
				implementation libs.testcontainers.mongodb
				implementation libs.rest.assured
			}

			targets {
				configureEach {
					testTask.configure {
						shouldRunAfter(test)
					}
				}
			}
		}
	}
}

tasks.named('check') {
	dependsOn(testing.suites.integrationTest)
}

jacocoTestReport {
	reports {
		xml.required
	}
	dependsOn test
}

//pitest {
//	junit5PluginVersion = '1.1.2'
//}

//check.dependsOn dependencyCheckAnalyze

flyway {
	url = 'jdbc:postgresql://localhost:5432/tom'
	user = 'tom'
	password = 'tom'
}

test {
    jvmArgs += [
            "--add-opens=java.base/java.lang=ALL-UNNAMED",
            "--add-opens=java.base/java.util=ALL-UNNAMED",
            "--add-opens=java.base/java.io=ALL-UNNAMED"
    ]
}
